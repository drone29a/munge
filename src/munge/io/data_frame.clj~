(ns munge.io.data-frame
  (:require [clojure.string :refer [split join trim]]
            [clojure.java.io :as io]
            [schema.core :as sc]
            [schema.macros :as sm]
            [schema.coerce :as s.coerce]))

(defn quoted-str [^String s] (and (.startsWith s "\"") (.endsWith s "\"")))

(def UnquotedStr (sc/pred #(and (string? %) (not (quoted-str %))) "unquoted-string"))
(def +unquoted-coercion+ {UnquotedStr (fn [^String s] (if (quoted-str s)
                                                (.substring s 1 (- (.length s) 1))
                                                s))})
(def +row-coercions+ (merge s.coerce/+string-coercions+
                            +unquoted-coercion+
                            {sc/Int (fn [^String x]
                                      (if (.equals "NA" x)
                                        -1
                                        ;;(s.coerce/edn-read-string x)
                                        (Integer/parseInt x)
                                        ))}))

(sm/defn read-data-line :- [sc/Str]
  [delimiter :- sc/Str
   line :- sc/Str]
  ;; A template for matching tokens in a record.
  ;; Substitute # for actual delimiter used.
  (let [pattern-template "([^\"#]+|\".*\"||\"\")[#\\newline]"
        p (-> (clojure.string/replace pattern-template #"#" delimiter)
              (re-pattern))]
    ;; TODO: Update regex to support lines with \n stripped.
    (map second (re-seq p (str line "\n")))))

;; TODO: not resilient to multiple line records
(sm/defn read-data-frame :- [[sc/Str]]
  [delimiter :- sc/Str
   header? :- sc/Bool
   col-names :- [(sc/either sc/Str sc/Keyword)]
   row-schema :- sc/Schema
   lines :- [sc/Str]]  
  (let [data (for [l lines] (read-data-line delimiter l))
        ;;data (for [l lines] (split l (re-pattern delimiter)))
        num-cols (-> data first count)
        default-col-names (vec (for [i (range num-cols)] (format "col-%d" i)))
        ;; Schema must not be a lazy-seq
        unquoted-row-schema (vec (for [c-name default-col-names] (sc/one UnquotedStr c-name)))
        col-names (or col-names default-col-names)
        row-schema (or row-schema unquoted-row-schema)
        parse-rows (s.coerce/coercer [row-schema] +row-coercions+)
        header (vec (map keyword (if header?
                                   ((s.coerce/coercer unquoted-row-schema +unquoted-coercion+) (first data))
                                   col-names)))
        rows (if header? (rest data) data)]
    (comment
      (->> rows
           (parse-rows)
           (map #(->> %
                      (vec)
                      (interleave header)
                      (apply assoc {})))
           (vec)))
    (->> rows
         (parse-rows)
         (map vec)
         (vec))))

;; TODO: Add delimiter escaping or value quoting, what does R do?
(defn write-data-frame [w headers records delimiter]
  (when headers
    (.write w (format "%s\n" (join delimiter headers))))
  (doseq [r records]
    (.write w (format "%s\n" (join delimiter r)))))

;; returns [(sc/either {sc/Keyword sc/Str} [sc/Str])]
(defn load-data-frame
  "Load a data frame from the given file.
  Arguments:
    delimiter - the delimiter used
    header? - does the first line provide column names
    col-names - column names to use, will override header names
    row-schema - types for the columns, will return strings if not provided"
  [path & {:keys [delimiter header? col-names row-schema]
           :or {delimiter ","
                header? false
                col-names nil
                row-schema nil}}] 
  (with-open [r (io/reader path)]
    (read-data-frame delimiter header? col-names row-schema (line-seq r))))

(defn save-data-frame [path headers records delimiter]
  (with-open [w (io/writer path)]
    (write-data-frame w headers records delimiter)))
